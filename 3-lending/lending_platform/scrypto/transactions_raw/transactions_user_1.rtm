# Withdrawing the user lending badge from the user account. When we call the `create_proof` methods on
# the account component the returned proof is automatically put in our auth zone so we do not need to
# manually manage it.
CALL_METHOD
    ComponentAddress("<<<account_1_address>>>")
    "create_proof"
    ResourceAddress("<<<account_1_lending_platform_badge>>>");

# The `create_proof` method returns as Proof. As soon as a proof comes back to the transaction worktop, it gets sent
# directly to the auth zone. Therefore, in the following instruction we're creating a `Proof` out of the badge in the
# auth zone.
CREATE_PROOF_FROM_AUTH_ZONE
    ResourceAddress("<<<account_1_lending_platform_badge>>>")
    Proof("user_badge");

# Add XRD to the worktop
CALL_METHOD
    ComponentAddress("<<<account_1_address>>>")
    "withdraw_by_amount"
    Decimal("1000")
    ResourceAddress("<<<xrd_token>>>");

# Put worktop XRD into a bucket
TAKE_FROM_WORKTOP_BY_AMOUNT
    Decimal("750")
    ResourceAddress("<<<xrd_token>>>")
    Bucket("deposit_1");

# Call `deposit_asset` to deposit the `deposit_1` XRD into the lending pool
CALL_METHOD
    ComponentAddress("<<<lending_platform_component_address>>>")
    "deposit_asset"
    Bucket("deposit_1")
    Proof("user_badge");

# The remaining 250 XRD are sent back to User 1
CALL_METHOD_WITH_ALL_RESOURCES
    ComponentAddress("<<<account_1_address>>>")
    "deposit_batch";
